/*
   *** CV-DATA-SPLITTER ***

   Package for splitting datasets for cross-validation purposes.

   written by: Artur Tarassow (atecon@posteo.de; https://github.com/atecon)
*/


function bundle CvDataSplitter (bundle b)

    /*=======================*/
    /* Main package function */
    /*=======================*/

    # Set up the bundle
    bundle self = default_cv_opts()
    if exists(b)
        # override defaults
        self = b + self
    endif

    # Initial checks
    #===============
    check_bundle(&self)

    # Drop missings
    #==============
    # TODO: think about how to handle NAs
    #    smpl --no-missing y X

    # Generate an observation index
    #===============================
    if !inbundle(self, "index") && (typeof(self.X)==7 || typeof(self.X)==2)
        genr index
        self.index = index		# index series for checking that values are related to the right unit
    endif

    # Call evaluation method
    #=======================
    if self.cv_type=="kfold" || self.cv_type=="loo"
        kfold(&self)
    elif self.cv_type=="recwin" || self.cv_type=="rolwin"
        timefold(&self)
    endif

    return self
end function


function bundle default_cv_opts (void)

    # Set default values of the bundle
    bundle self = null
    string self.cv_type = "kfold"		# "kfold", "rep_kfold", "loo", "recwin", "rolwin"
    scalar self.n_folds = 5				# divide sample into k groups of samples (default for kfold = 5)
    scalar self.win_size = int(0.25*$nobs)	# for 'recwin'/'rolwin': initial window lenght

    return self
end function


function void check_bundle (bundle *self)
    /* Some initial checks for bundle completeness */

    if !inbundle(self, "X")
        funcerr "Provide a list or matrix with data entitled 'X'."
    endif
    if typeof(self.X)==3		# if matrix
        if !inbundle(self, "index")
            funcerr "Provide a vector holding index observations values (via 'genr index') named 'index'."
        elif inbundle(self, "index")
            if typeof(self.index)!=3
                funcerr "Make sure your object 'index' is of type matrix (row vector)."
            endif
        endif
    endif
end function


function bundle gen_xmatrices (bundle *self)
/* Helper function for generating an
    array for storing the output */
    matrices self.X_train = array(self.n_folds)
    matrices self.X_test = array(self.n_folds)
    return self
end function


function bundle kfold (bundle *self)
/* Function for running kfold and loo

   Splits data in train/test sets.
   Split dataset into k consecutive folds (without shuffling).
   Each fold can be used once as a validation while the k - 1 remaining
   folds form the training set.

   'loo' is a special case of 'kfold' with a single obs. left-out for each fold

   See: https://scikit-learn.org/stable/modules/generated/sklearn.model_selection.KFold.html
    */


    self.n_folds = (self.cv_type=="loo") ? $nobs : self.n_folds

    # Read-in frame
    X = self.X
    # Index
    if typeof(X)==2 || typeof(X)==7
        series index = self.index
    else
        matrix index  = self.index
    endif

    # Generate test/trainig arrays
    gen_xmatrices(&self)

    # Lenght of each test sample
    scalar T = (typeof(X)==7) ? $nobs : rows(X)
    scalar self.foldsize = int( T/self.n_folds )		# lengt of test sample
    scalar rest = T - self.n_folds*self.foldsize		# no. of remainder obs for last iteration

    # Initial test set indices
    scalar start = min(index)
    scalar ende = (start+self.foldsize)-1

    # Start looping
    loop i=1..self.n_folds -q
        #printf "\nStart=%d \t Ende=%d\n", start, ende
        
        # Test set
        if typeof(X)==7								# For list-based approach
            series active_set = 0					# don't drop: indicator for current sample
            smpl index>=start && index<=ende --restrict
            active_set = 1						# don't drop
            self.X_test[i] = {index} ~ {X}

        else					# For matrix-based approach
            matrix active_set = (index.>=start && index.<=ende)
            self.X_test[i] = selifr(index,active_set) ~ selifr(X,active_set)
        endif

        # Training set
        if typeof(X)==7			# For list-based approach
            smpl active_set==0 --restrict --replace
            self.X_train[i] = {index} ~ {X}
            smpl full

        else
            active_set = (active_set.=1) ? 0 : 1				# select training set
            self.X_train[i] = selifr(index,active_set) ~ selifr(X,active_set)
        endif

        # update start/ ende
        start += self.foldsize
        ende = start + self.foldsize-1
        ende = (i==self.n_folds) ? (ende+rest) : ende
    endloop

    return self
end function


function bundle timefold (bundle *self)
/* Function for either rolling or recursive window splits

   See: https://scikit-learn.org/stable/modules/generated/sklearn.model_selection.TimeSeriesSplit.html#sklearn.model_selection.TimeSeriesSplit
    */

    # Rsead-in frame
    X = self.X
    # Index
    if typeof(X)==2 || typeof(X)==7
        series index = self.index
    else
        matrix index  = self.index
    endif

    # Lenght of each test sample
    scalar T = (typeof(X)==7) ? $nobs : rows(X)
    scalar self.foldsize = self.win_size			# lengt of (initial) test sample
    #    scalar rest = T-self.n_folds*self.foldsize	# no. of remainder obs for last iteration

    # Generate test/trainig arrays
    scalar self.n_folds = T-self.foldsize		# T-1 training sets can be compiled
    gen_xmatrices(&self)

    # Initial test set indices
    scalar start = min(index)
    scalar ende = (start+self.foldsize)-1

    # Start looping
    loop i=1..self.n_folds -q
        printf "\nStart=%d \t Ende=%d\n", start, ende
        
        # Training set
        if typeof(X)==7								# For list-based approach
            series active_set = 0					# don't drop: indicator for current sample
            smpl index>=start && index<=ende --restrict --replace
            active_set = 1						# don't drop
            self.X_train[i] = {index} ~ {X}

        else					# For matrix-based approach
            matrix active_set = (index.>=start && index.<=ende)
            self.X_train[i] = selifr(index,active_set) ~ selifr(X,active_set)
        endif

        # Test set
        if typeof(X)==7			# For list-based approach
            smpl index>ende --restrict --replace
            self.X_test[i] = {index} ~ {X}
            smpl full

        else
            active_set = (index.>ende)
            self.X_test[i] = selifr(index,active_set) ~ selifr(X,active_set)
        endif

        # update start/ ende
        start = (self.cv_type=="rolwin") ? (start+1) : start
        ende++
    endloop
    
    return self
end function
